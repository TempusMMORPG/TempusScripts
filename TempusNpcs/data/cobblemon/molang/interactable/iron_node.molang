!c.player.main_held_item.is_in('minecraft:pickaxes') ? {
    c.player.tell('§cYou need a §6Pickaxe §cto mine this');
    return 0;
};

t.time = c.player.world.game_time;
t.next_mine_time = c.player.get_npc_variable(q.npc.uuid, 'next_mine_time');

t.time < t.next_mine_time ? {
    return 0;
};

q.set_query('player', c.player);

t.max_health = q.npc.config.node_health;
t.health = q.player.get_npc_variable(q.npc.uuid, 'node_health');

t.health == 0 ? {
    t.health = t.max_health;
};

t.health = t.health - 1;
q.npc.play_animation('hit', q.player.uuid);
q.run_command('playsound minecraft:block.deepslate.place master ' + q.player.username + ' ' + q.npc.x + ' ' + q.npc.y + ' ' + q.npc.z);

t.health <= 0 ? {
    q.run_script('cobblemon:iron_node_reward');
    q.run_command('playsound cobblemon:block.display_case.add_item master ' + q.player.username + ' ' + q.npc.x + ' ' + q.npc.y + ' ' + q.npc.z);
    t.health = t.max_health;
};

q.player.tell('§aMining: ' + q.npc.name + ' §6' + t.health + ' §ahealth left!');

t.cooldown = t.time + (q.npc.config.node_cooldown * 20);

q.player.set_npc_variable(q.npc.uuid, 'node_health', t.health);
q.player.set_npc_variable(q.npc.uuid, 'next_mine_time', t.cooldown);
q.player.save_data();